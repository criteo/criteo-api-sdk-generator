import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo1.maven.org/maven2" }
    }

    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:5.2.1"
    }
}

apply plugin: 'org.openapi.generator'

def clientOutputDir = "$projectDir/../../$rootProject.gereratedClientsDirName/java".toString()
def resourcesDir = "$projectDir/resources".toString()
def basePackage = "com.criteo.api"
def basePackagePath = basePackage.replace(".", "/")

task cleanPreviousJavaBuild(type: Delete) {
    group 'Criteo'
    description 'Clean up generated client output folder'

    delete clientOutputDir
    followSymlinks = true
}

task generateClient(type: GradleBuild) {
    group "Criteo"
    description 'Generate the Java client using openapi-generator and custom templates'
	
    def taskList = ['cleanPreviousJavaBuild'];

	rootProject.swaggerSourceList.each {
        def apiName = it.getName().replace(".json", "").toLowerCase();
        def jsonFile = file(it.path);
        def parsedJson = new groovy.json.JsonSlurper().parseText(jsonFile.text);
        def clientOutputDirPerVersion = clientOutputDir + "/" + it.getName().replace(".json", "");
        def specPath = it.path;

        def generateTask = task("openApiGenerate_java_" + apiName, type: GenerateTask.class) {
            generatorName = "java"
            templateDir = "$resourcesDir/templates/".toString()
            inputSpec = specPath
            outputDir = clientOutputDirPerVersion
            removeOperationIdPrefix = true
            generateApiTests = false
            generateModelTests = false
            configOptions = [
                    groupId                  : 'com.criteo',
                    apiPackage               : "${basePackage}.api".toString(),
                    artifactId               : 'api.java-client',
                    apiVersion               : parsedJson.info.version,
                    artifactDescription      : 'Criteo API SDK for Java',
                    artifactUrl              : 'https://github.com/criteo/criteo-api-java-sdk',
                    modelPackage             : "${basePackage}.model".toString(),
                    scmConnection            : 'scm:git:git://github.com/criteo/criteo-api-java-sdk.git',
                    scmDeveloperConnection   : 'scm:git:ssh://github.com:criteo/criteo-api-java-sdk.git',
                    scmUrl                   : 'https://github.com/criteo/criteo-api-java-sdk',
                    developerName            : 'Criteo',
                    developerEmail           : 'open-source@criteo.com',
                    developerOrganization    : 'Criteo',
                    developerOrganizationUrl : 'https://www.criteo.com/',
                    licenseName              : 'Apache License, version 2.0',
                    licenseUrl               : 'https://www.apache.org/licenses/LICENSE-2.0.txt',
                    hideGenerationTimestamp  : 'true',
                    dateLibrary              : 'java8',
            ]
        }
        def copyLicense = task("copyLicense_java_" + apiName, type: Copy) {
            group 'Criteo'
            description 'Copy the license into the client output folder'

            from "$projectDir/../LICENSE"
            into clientOutputDirPerVersion
            rename 'LICENSE', 'LICENSE.txt'
        };

        def copyExamples = task("copyExamples_java_"+ apiName, type: Copy) {
                                 from "$resourcesDir/examples"
                                 into "$clientOutputDirPerVersion/src/examples/java/$basePackagePath/examples".toString()
        }

        def removeUnwantedFiles = task("removeUnwantedFiles_java_" + apiName, type: Delete) {
            group 'Criteo'
            description 'Remove files generated by openapi-generator that we don\'t want to push'

            delete fileTree(clientOutputDirPerVersion) {
                include '.openapi-generator-ignore', '.travis.yml', 'git_push.sh',\
                        'build.sbt', 'src/main/AndroidManifest.xml', 'gradle.properties'
            }
            followSymlinks = true
        }

        taskList << generateTask.name
        taskList << copyLicense.name
        taskList << copyExamples.name
        taskList << removeUnwantedFiles.name
    }

    generateClient.dependsOn(taskList);
}

def clientPatch = hasProperty('buildNumber') ? buildNumber : '0'