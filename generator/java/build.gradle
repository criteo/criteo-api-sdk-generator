import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo1.maven.org/maven2" }
    }

    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:5.2.1"
    }
}

apply plugin: 'org.openapi.generator'

def clientOutputDir = "$projectDir/../../$rootProject.gereratedClientsDirName/java".toString()
def resourcesDir = "$projectDir/resources".toString()
def criteoPackage = project.properties["package.name.base"]
def basePackagePath = criteoPackage.replace(".", "/")
def generatorVersion = project.properties["generator.version"]
def technologyStack = "java8"
def nameSeparator = '.'

task cleanPreviousJavaBuild(type: Delete) {
    group 'Criteo'
    description 'Clean up generated client output folder'

    delete clientOutputDir
    followSymlinks = true
}

task generateClient(type: GradleBuild) {
    group "Criteo"
    description 'Generate the Java client using openapi-generator and custom templates'
	
    def taskList = ['cleanPreviousJavaBuild'];

	rootProject.swaggerSourceList.each {
        def jsonFile = file(it.path);
        def parsedJson = new groovy.json.JsonSlurper().parseText(jsonFile.text);

        def parsedApiName = it.getName().replace(".json", "").toLowerCase();
        def (criteoService, apiVersionFromName) = parsedApiName.tokenize('_');
        def criteoApiVersion = apiVersionFromName.replace('-', '_');
        
        def clientOutputDirPerVersion = clientOutputDir + "/" + parsedApiName;
        def specPath = it.path;
        def packageBase =  criteoApiVersion; 
        if (packageBase.matches("[0-9].*"))
             packageBase= "v" + packageBase;

        packageBase = "${criteoPackage}.${criteoService}.${packageBase}".replace('.', nameSeparator).toString(); // com.criteo.api.marketingsolutions.v2021_10
        def packageVersion = "${criteoApiVersion}.${generatorVersion}.${getFormattedDate()}";
        
        packageBase = "com." + packageBase; // Java-specific

        def generateTask = task("openApiGenerate_java_" + parsedApiName, type: GenerateTask.class) {
            generatorName = "java"
            templateDir = "$resourcesDir/templates/".toString()
            inputSpec = specPath
            outputDir = clientOutputDirPerVersion
            removeOperationIdPrefix = true
            generateApiTests = false
            generateModelTests = false
            configOptions = [
                    groupId                  : 'com.criteo',
                    apiPackage               : "${packageBase}.api".toString(), // com.criteo.api.marketingsolutions.v2021_10.api
                    artifactId               : "criteo-api-${criteoService}-sdk-${technologyStack}".toString(), // criteo-api-marketingsolutions-sdk-java8
                    artifactVersion          : "${packageVersion}".toString(), // v2021_10.1.211109 
                    apiVersion               : parsedJson.info.version,  // 2021-10
                    artifactDescription      : "Java SDK for Criteo API ${criteoService} for ${criteoApiVersion} version".toString(),
                    artifactUrl              : 'https://github.com/criteo/criteo-api-java-sdk',
                    modelPackage             : "${packageBase}.model".toString(),
                    scmConnection            : 'scm:git:git://github.com/criteo/criteo-api-java-sdk.git',
                    scmDeveloperConnection   : 'scm:git:ssh://github.com:criteo/criteo-api-java-sdk.git',
                    scmUrl                   : 'https://github.com/criteo/criteo-api-java-sdk',
                    developerName            : 'Criteo',
                    developerEmail           : 'open-source@criteo.com',
                    developerOrganization    : 'Criteo',
                    developerOrganizationUrl : 'https://www.criteo.com/',
                    licenseName              : 'Apache License, version 2.0',
                    licenseUrl               : 'https://www.apache.org/licenses/LICENSE-2.0.txt',
                    hideGenerationTimestamp  : 'true',
                    dateLibrary              : technologyStack.toString(),
            ]
        }
        def copyLicense = task("copyLicense_java_" + parsedApiName, type: Copy) {
            group 'Criteo'
            description 'Copy the license into the client output folder'

            from "$projectDir/../LICENSE"
            into clientOutputDirPerVersion
            rename 'LICENSE', 'LICENSE.txt'
        };

        def copyExamples = task("copyExamples_java_"+ parsedApiName, type: Copy) {
                                 from "$resourcesDir/examples"
                                 into "$clientOutputDirPerVersion/src/examples/java/$basePackagePath/examples".toString()
        }

        def removeUnwantedFiles = task("removeUnwantedFiles_java_" + parsedApiName, type: Delete) {
            group 'Criteo'
            description 'Remove files generated by openapi-generator that we don\'t want to push'

            delete fileTree(clientOutputDirPerVersion) {
                include '.openapi-generator-ignore', '.travis.yml', 'git_push.sh',\
                        'build.sbt', 'src/main/AndroidManifest.xml', 'gradle.properties'
            }
            followSymlinks = true
        }

        taskList << generateTask.name
        taskList << copyLicense.name
        taskList << copyExamples.name
        taskList << removeUnwantedFiles.name
    }

    generateClient.dependsOn(taskList);
}

def clientPatch = hasProperty('buildNumber') ? buildNumber : '0'

def getFormattedDate() {
    def date = new Date()
    def formattedDate = date.format('yyMMdd')
    return formattedDate
}
